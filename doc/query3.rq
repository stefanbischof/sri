PREFIX sri: <https://w3id.org/sri#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX prov: <http://www.w3.org/ns/prov#>
# Step 3 in the SRI methodology
# documented in section 1.3.6.3 on page 129 in the final report
# available from https://data.europa.eu/doi/10.2833/41100
# "Calculate impact criterion scores"
INSERT {
    GRAPH $scoresds {
        ?scoresobs a qb:Observation ;
             qb:dataSet $scoresds ;
             sri:function ?domain ;
             sri:impact ?impact ;
             sri:icscore ?discore .

        $scoresds a qb:DataSet , prov:Entity ;
             qb:structure sri:dsd-scores ;
             sri:building ?building ;
             prov:wasDerivedFrom $ds ;
             prov:wasAttributedTo <https://uri.of.calculation.agent/> .
    }
}
WHERE {
    {
        SELECT ?domain ?impact (SUM(?score*?share + ?addscore*(1.0-?share)) as ?discore) (UUID() as ?scoresobs)
        WHERE {
            # assessment
            $ds qb:structure sri:dsd-assessment ;
                sri:serviceCatalogue ?sc .
            ?levelobs qb:dataSet $ds ;
                    sri:function ?service ;
                    sri:level ?level .

            # SRI service score
            [] qb:dataSet ?refscoresds ;
                sri:function ?service ;
                sri:impact ?impact ;
                sri:level ?level ;
                sri:icscore ?score .
            ?domain skos:narrower ?service .
            ?sc sri:scores ?refscoresds .

            # in case of additional level
            OPTIONAL {
                ?levelobs sri:share ?share ;
                    sri:additionalLevel ?addlevel .

                # SRI service score additional level
                [] qb:dataSet ?refscoresds ;
                    sri:function ?service ;
                    sri:impact ?impact ;
                    sri:level ?addlevel ;
                    sri:icscore ?addscore .
                }
            # neutral default values in case no additional level present
            OPTIONAL {
                BIND(1.0 as ?share)
                BIND(0 as ?addscore) }
        }
        GROUP BY ?domain ?impact #$ds
    }

    $ds sri:building ?building .
}
