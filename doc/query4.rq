PREFIX sri: <https://w3id.org/sri#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX prov: <http://www.w3.org/ns/prov#>
# Step 4 in the SRI methodology
# documented in section 1.3.6.4 on page 130 in the final report
# available from https://data.europa.eu/doi/10.2833/41100
# "Calculate maximum impact scores"
INSERT {
    GRAPH $scoresds {
        ?scoresobsdi sri:max ?maxscore .
    }
}
WHERE {
    {
        SELECT ?domain ?impact (sum(?max) as ?maxscore) # aggregate from services to domains
        WHERE {
            {   SELECT ?service ?impact (max(?score) as ?max) # get maximum score of assessed service for each service
                WHERE {
                    [] qb:dataSet ?refscoresds ;
                        sri:function ?service ;
                        sri:impact ?impact ;
                        sri:score ?score .
                    
                    ?service a sri:Service .
                    ?sc sri:scores ?refscoresds .

                    GRAPH $scoresds { # get the the assessment dataset ?assds
                        $scoresds prov:wasDerivedFrom ?assds . 
                    }
                    
                    GRAPH ?assds { # check if the assessment contains the service
                        ?assds qb:structure sri:dsd-assessment ;
                            sri:serviceCatalogue ?sc .
                        [] qb:dataSet ?assds ;
                            sri:function ?service  .   
                    }
                } GROUP BY ?impact ?service
            }
            ?domain skos:narrower ?service .
        } GROUP BY ?impact ?domain
    }

    GRAPH $scoresds { # get corresponding observation
        ?scoresobsdi
            qb:dataSet $scoresds ;
            sri:function ?domain ;
            sri:impact ?impact .
    }
}
